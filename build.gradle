import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("com.diffplug.spotless:spotless-plugin-gradle:${Version.SPOTLESS}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${Version.SPRING_BOOT}")
    }
}

version = Git.commitHash()

subprojects { Project project ->
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    sourceCompatibility = 17
    targetCompatibility = 17
    version Git.commitHash()

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${Version.SPRING_BOOT}") }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()

        testLogging {
            events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
        }
    }

    spotless {
        java {
            googleJavaFormat()
        }
    }
}